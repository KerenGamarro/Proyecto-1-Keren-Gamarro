//******************************************
// Universidad del Valle de Guatemala
// BE3029 - Electronica Digital 2
// Keren Gamarro
// 25/08/2025
// Proyecto 1 - LM35 con servo tipo semáforo + Display 7 segmentos
//******************************************

#include <Arduino.h>
#include <stdint.h>

//---------------------------
// Pines LM35 y control
//---------------------------
#define LM35_PIN      34   // Entrada analógica del sensor LM35
#define BOTON_PIN     33   // Botón para activar lectura de temperatura
#define LED_AMARILLO  27   // LED para zona baja
#define LED_NARANJA   26   // LED para zona media
#define LED_ROJO      25   // LED para zona alta

//---------------------------
// Configuración del Servo
//---------------------------
#define SERVO_PIN     13   // Pin del servo
#define SERVO_CANAL    0   // Canal PWM usado
#define SERVO_FREQ    50   // Frecuencia de 50 Hz (estándar en servos)
#define SERVO_RES     16   // Resolución de 16 bits

//---------------------------
// Segmentos del display 7 segmentos
// Cátodo común
//---------------------------
#define SEG_A 23
#define SEG_B 14
#define SEG_C 18
#define SEG_D 15
#define SEG_E 5
#define SEG_F 12
#define SEG_G 4
#define SEG_P 2   // Punto decimal

// Pines de selección de dígito (multiplexeo)
#define DIG1 22
#define DIG2 21
#define DIG3 19
// (el 4º dígito no se usa en este proyecto)

//---------------------------
// Variables globales
//---------------------------
bool botonPresionado = false;  // Bandera activada por ISR
float factorCalibracion = 1.0; // Ajuste de temperatura
float ultimaTemp = 0;          // Última temperatura leída

// Ángulos fijos para cada zona de temperatura
const int servoAmarillo = 30;  // Zona baja (<22 °C)
const int servoNaranja  = 90;  // Zona media (22–25 °C)
const int servoRojo     = 150; // Zona alta (>25 °C)
const int servoMin      = 0;   // Posición inicial

//---------------------------
// Tabla de números → segmentos
// [a b c d e f g]
//---------------------------
const byte numToSeg[10][7] = {
  {1,1,1,1,1,1,0}, // 0
  {0,1,1,0,0,0,0}, // 1
  {1,1,0,1,1,0,1}, // 2
  {1,1,1,1,0,0,1}, // 3
  {0,1,1,0,0,1,1}, // 4
  {1,0,1,1,0,1,1}, // 5
  {1,0,1,1,1,1,1}, // 6
  {1,1,1,0,0,0,0}, // 7
  {1,1,1,1,1,1,1}, // 8
  {1,1,1,1,0,1,1}  // 9
};

//---------------------------
// ISR del botón
//---------------------------
void IRAM_ATTR botonISR() {
  botonPresionado = true;  // Marca que el botón fue presionado
}

//---------------------------
// Función mover servo según zona
//---------------------------
void moverServoPorZona(float tempC) {
  int duty = 0;

  if (tempC < 22.0) {
    duty = map(servoAmarillo, 0, 180, 1638, 7864);
  } else if (tempC <= 25.0) {
    duty = map(servoNaranja, 0, 180, 1638, 7864);
  } else {
    duty = map(servoRojo, 0, 180, 1638, 7864);
  }

  ledcWrite(SERVO_CANAL, duty); // Aplica el valor PWM al servo
}

//---------------------------
// Encender un número en segmentos
// num: dígito 0–9
// punto: true si se activa el punto decimal
//---------------------------
void mostrarDigito(int num, bool punto) {
  digitalWrite(SEG_A, numToSeg[num][0]);
  digitalWrite(SEG_B, numToSeg[num][1]);
  digitalWrite(SEG_C, numToSeg[num][2]);
  digitalWrite(SEG_D, numToSeg[num][3]);
  digitalWrite(SEG_E, numToSeg[num][4]);
  digitalWrite(SEG_F, numToSeg[num][5]);
  digitalWrite(SEG_G, numToSeg[num][6]);
  digitalWrite(SEG_P, punto ? HIGH : LOW);
}

//---------------------------
// Mostrar temperatura con multiplexeo
// temp → se muestran decenas, unidades y décimas
//---------------------------
void mostrarNumero(float temp) {
  int dec  = (int)temp / 10;         // decenas
  int uni  = (int)temp % 10;         // unidades
  int deci = (int)(temp * 10) % 10;  // primer decimal

  // --- Dígito 1: decenas ---
  digitalWrite(DIG1, HIGH);
  mostrarDigito(dec, false);
  delay(3);
  digitalWrite(DIG1, LOW);

  // --- Dígito 2: unidades + punto decimal ---
  digitalWrite(DIG2, HIGH);
  mostrarDigito(uni, true);
  delay(3);
  digitalWrite(DIG2, LOW);

  // --- Dígito 3: décimas ---
  digitalWrite(DIG3, HIGH);
  mostrarDigito(deci, false);
  delay(3);
  digitalWrite(DIG3, LOW);
}

//---------------------------
// Setup (inicialización)
//---------------------------
void setup() {
  Serial.begin(115200);

  // Pines sensores y LEDs
  pinMode(LM35_PIN, INPUT);
  pinMode(BOTON_PIN, INPUT_PULLUP);
  pinMode(LED_AMARILLO, OUTPUT);
  pinMode(LED_NARANJA, OUTPUT);
  pinMode(LED_ROJO, OUTPUT);

  // Pines display 7 segmentos
  pinMode(SEG_A, OUTPUT);
  pinMode(SEG_B, OUTPUT);
  pinMode(SEG_C, OUTPUT);
  pinMode(SEG_D, OUTPUT);
  pinMode(SEG_E, OUTPUT);
  pinMode(SEG_F, OUTPUT);
  pinMode(SEG_G, OUTPUT);
  pinMode(SEG_P, OUTPUT);

  pinMode(DIG1, OUTPUT);
  pinMode(DIG2, OUTPUT);
  pinMode(DIG3, OUTPUT);

  // Configuración del servo
  ledcSetup(SERVO_CANAL, SERVO_FREQ, SERVO_RES);
  ledcAttachPin(SERVO_PIN, SERVO_CANAL);
  ledcWrite(SERVO_CANAL, map(servoMin, 0, 180, 1638, 7864));

  // Interrupción para botón
  attachInterrupt(digitalPinToInterrupt(BOTON_PIN), botonISR, FALLING);

  Serial.println("Sistema listo.");
}

//---------------------------
// Loop principal
//---------------------------
void loop() {
  // Mostrar siempre la última temperatura en el display
  mostrarNumero(ultimaTemp);

  // Si se presionó el botón, se toma nueva medida
  if (botonPresionado) {
    botonPresionado = false;

    // --- Promediar lecturas del ADC ---
    long suma = 0;
    for (int i = 0; i < 50; i++) {
      suma += analogRead(LM35_PIN);
      delay(2); // breve pausa entre lecturas
    }
    unsigned int valorADC = suma / 50;

    // Conversión ADC → temperatura (LM35: 10 mV/°C)
    float tempC = (valorADC * (5.0 / 4095.0)) * 100.0;
    tempC = tempC * factorCalibracion; // aplicar calibración
    ultimaTemp = tempC; // guardar valor para display

    // Mostrar en monitor serie
    Serial.print("Temperatura filtrada: ");
    Serial.print(tempC, 1);
    Serial.println(" ºC");

    // --- Semáforo de LEDs ---
    digitalWrite(LED_AMARILLO, LOW);
    digitalWrite(LED_NARANJA, LOW);
    digitalWrite(LED_ROJO, LOW);

    if (tempC < 22.0) digitalWrite(LED_AMARILLO, HIGH);
    else if (tempC <= 25.0) digitalWrite(LED_NARANJA, HIGH);
    else digitalWrite(LED_ROJO, HIGH);

    // --- Mover servo según zona ---
    moverServoPorZona(tempC);
  }
}
