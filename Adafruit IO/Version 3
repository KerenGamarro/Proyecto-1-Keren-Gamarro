//******************************************
// Universidad del Valle de Guatemala
// BE3029 - Electronica Digital 2
// Keren Gamarro
// 25/08/2025
// Proyecto 1 - LM35 con servo tipo semáforo + Display 7 segmentos + Adafruit IO
// Multiplexeo optimizado para display estable
//******************************************

#include <Arduino.h>
#include <stdint.h>
#include "WiFi.h"
#include "AdafruitIO_WiFi.h"

// ---------------------------
// Adafruit IO (credenciales)
// ---------------------------
#define IO_USERNAME  "Keren_Gabriela"
#define IO_KEY       "io_GBqo77aCGsP0YapYLvDRMRorXhT4" //solo agregar a

// WiFi
#define WIFI_SSID    "Keren Gabriela"
#define WIFI_PASS    "kaylaythorin04"

// Cliente Adafruit IO
AdafruitIO_WiFi io(IO_USERNAME, IO_KEY, WIFI_SSID, WIFI_PASS);

// Feed de temperatura
AdafruitIO_Feed *tempFeed = io.feed("Temperatura");

// ---------------------------
// Pines LM35 y control
// ---------------------------
#define LM35_PIN      34
#define BOTON_PIN     33
#define LED_AMARILLO  27
#define LED_NARANJA   26
#define LED_ROJO      25

// ---------------------------
// Configuración del Servo
// ---------------------------
#define SERVO_PIN     13
#define SERVO_CANAL    0
#define SERVO_FREQ    50
#define SERVO_RES     16

// ---------------------------
// Segmentos display 7 seg
// ---------------------------
#define SEG_A 23
#define SEG_B 14
#define SEG_C 18
#define SEG_D 15
#define SEG_E 5
#define SEG_F 12
#define SEG_G 4
#define SEG_P 2

#define DIG1 22
#define DIG2 21
#define DIG3 19

// ---------------------------
// Variables globales
// ---------------------------
bool botonPresionado = false;
float factorCalibracion = 1.0;
float ultimaTemp = 0.0;
float alpha = 0.1;

// Ángulos servo
const int servoAmarillo = 30;
const int servoNaranja  = 90;
const int servoRojo     = 150;
const int servoMin      = 0;

// Tabla de números
const byte numToSeg[10][7] = {
  {1,1,1,1,1,1,0}, {0,1,1,0,0,0,0}, {1,1,0,1,1,0,1}, {1,1,1,1,0,0,1},
  {0,1,1,0,0,1,1}, {1,0,1,1,0,1,1}, {1,0,1,1,1,1,1}, {1,1,1,0,0,0,0},
  {1,1,1,1,1,1,1}, {1,1,1,1,0,1,1}
};

// ---------------------------
// ISR del botón
// ---------------------------
void IRAM_ATTR botonISR() {
  botonPresionado = true;
}

// ---------------------------
// Servo según zona
// ---------------------------
void moverServoPorZona(float tempC) {
  int duty = 0;
  if (tempC < 22.0) duty = map(servoAmarillo, 0, 180, 1638, 7864);
  else if (tempC <= 25.0) duty = map(servoNaranja, 0, 180, 1638, 7864);
  else duty = map(servoRojo, 0, 180, 1638, 7864);
  ledcWrite(SERVO_CANAL, duty);
}

// ---------------------------
// Encender dígito
// ---------------------------
void mostrarDigito(int num, bool punto) {
  digitalWrite(SEG_A, numToSeg[num][0]);
  digitalWrite(SEG_B, numToSeg[num][1]);
  digitalWrite(SEG_C, numToSeg[num][2]);
  digitalWrite(SEG_D, numToSeg[num][3]);
  digitalWrite(SEG_E, numToSeg[num][4]);
  digitalWrite(SEG_F, numToSeg[num][5]);
  digitalWrite(SEG_G, numToSeg[num][6]);
  digitalWrite(SEG_P, punto ? HIGH : LOW);
}

// ---------------------------
// Mostrar temperatura con multiplexeo estable
// ---------------------------
void mostrarNumero(float temp) {
  int dec  = (int)temp / 10;
  int uni  = (int)temp % 10;
  int deci = (int)(temp * 10) % 10;

  digitalWrite(DIG1, HIGH); mostrarDigito(dec, false); delay(3); digitalWrite(DIG1, LOW);
  digitalWrite(DIG2, HIGH); mostrarDigito(uni, true);  delay(3); digitalWrite(DIG2, LOW);
  digitalWrite(DIG3, HIGH); mostrarDigito(deci,false);  delay(3); digitalWrite(DIG3, LOW);
}

// ---------------------------
// Setup
// ---------------------------
void setup() {
  Serial.begin(115200);

  pinMode(LM35_PIN, INPUT);
  pinMode(BOTON_PIN, INPUT_PULLUP);
  pinMode(LED_AMARILLO, OUTPUT);
  pinMode(LED_NARANJA, OUTPUT);
  pinMode(LED_ROJO, OUTPUT);

  pinMode(SEG_A, OUTPUT); pinMode(SEG_B, OUTPUT); pinMode(SEG_C, OUTPUT);
  pinMode(SEG_D, OUTPUT); pinMode(SEG_E, OUTPUT); pinMode(SEG_F, OUTPUT);
  pinMode(SEG_G, OUTPUT); pinMode(SEG_P, OUTPUT);

  pinMode(DIG1, OUTPUT); pinMode(DIG2, OUTPUT); pinMode(DIG3, OUTPUT);

  ledcSetup(SERVO_CANAL, SERVO_FREQ, SERVO_RES);
  ledcAttachPin(SERVO_PIN, SERVO_CANAL);
  ledcWrite(SERVO_CANAL, map(servoMin, 0, 180, 1638, 7864));

  attachInterrupt(digitalPinToInterrupt(BOTON_PIN), botonISR, FALLING);

  // --- Conexión a Adafruit IO ---
  Serial.print("Conectando a Adafruit IO...");
  io.connect();
  while(io.status() < AIO_CONNECTED) {
    Serial.print(".");
    delay(500);
  }
  Serial.println(" conectado!");
}

// ---------------------------
// Loop
// ---------------------------
void loop() {
  io.run();  
  mostrarNumero(ultimaTemp);  // Mostrar display estable

  if (botonPresionado) {
    botonPresionado = false;

    long suma = 0;
    for (int i = 0; i < 50; i++) {
      suma += analogRead(LM35_PIN);
      delay(2);
    }
    unsigned int valorADC = suma / 50;

    float tempC = (valorADC * (5.0 / 4095.0)) * 100.0;
    tempC = tempC * factorCalibracion;

    ultimaTemp = (alpha * tempC) + ((1 - alpha) * ultimaTemp);

    Serial.print("Temperatura filtrada: ");
    Serial.print(ultimaTemp, 1);
    Serial.println(" ºC");

    tempFeed->save(ultimaTemp);

    digitalWrite(LED_AMARILLO, LOW);
    digitalWrite(LED_NARANJA, LOW);
    digitalWrite(LED_ROJO, LOW);
    if (ultimaTemp < 22.0) digitalWrite(LED_AMARILLO, HIGH);
    else if (ultimaTemp <= 25.0) digitalWrite(LED_NARANJA, HIGH);
    else digitalWrite(LED_ROJO, HIGH);

    moverServoPorZona(ultimaTemp);
  }
}
